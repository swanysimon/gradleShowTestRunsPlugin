import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id "com.gradle.plugin-publish"
    id "groovy"
    id "java-gradle-plugin"
    id "maven-publish"
}

repositories {
    jcenter()
}

version = "0.0.0"
group = "swanysimon.showTestRuns"

dependencies {
    testImplementation gradleApi()
    testImplementation("org.spockframework:spock-core:1.3-groovy-2.5") {
        exclude group: "org.codehaus.groovy"
    }
}

tasks.withType(Test).configureEach {
    def testLogging = it.testLogging
    testLogging.exceptionFormat = TestExceptionFormat.FULL
    testLogging.events += TestLogEvent.FAILED

    LogLevel.values().each {
        testLogging.get(it).exceptionFormat = TestExceptionFormat.FULL
        testLogging.get(it).events += TestLogEvent.FAILED
    }

    it.afterSuite { desc, result ->
        // only report results for topmost module or suite
        if (desc.parent) {
            return
        }

        logger.lifecycle(
                "Test Results: {} {} tests, {} passed, {} skipped, {} failed",
                result.resultType,
                result.testCount,
                result.successfulTestCount,
                result.skippedTestCount,
                result.failedTestCount)
    }
}

gradlePlugin {
    plugins {
        showTestRuns {
            id = "swanysimon.show-test-runs"
            implementationClass = "swanysimon.showTestRuns.ShowTestRunsPlugin"
            description = "Increases console output during test runs."
        }
    }
}

pluginBundle {
    website = "https://github.com/swanysimon/gradleShowTestRunsPlugin"
    vcsUrl = "https://github.com/swanysimon/gradleShowTestRunsPlugin.git"
    tags = ["testing"]

    plugins {
        showTestRuns {
            displayName = "Show Test Runs plugin"
        }
    }
}

tasks.named("publishPlugins").configure {
    onlyIf {
        def rootProps = rootProject.properties
        return rootProps["gradle.publish.key"] && rootProps["gradle.publish.secret"]
    }
}
tasks.named("publish").configure { it.dependsOn tasks.named("publishPlugins") }
